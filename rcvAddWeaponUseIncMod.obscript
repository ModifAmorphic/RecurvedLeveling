scn rcvAddWeaponUseIncMod

ref weapon
int weaponType
int skillCode
float speed
float damage
float dps
float normalizeToDps
float expPerSec
float expDpsMod
float useIncMod

Begin _Function { }

    ; Disabled, return
    if (Recurved.WeaponExpNormalizedDisabled)
        return
    endif
    
    set weapon to Player.GetEquippedObject 16
    set weaponType to GetWeaponType weapon
    ; Weapon Types
    ; 0: Blade1H
    ; 1: Blade2H
    ; 2: Blunt1H
    ; 3: Blunt2H
    ; 4: Staff
    ; 5: Bow

    if (weaponType < 2)
        set skillCode to 17 ; Blade
    elseif (weaponType < 5)
        set skillCode to 16 ; Blunt
    elseif (weaponType == 5)
        set skillCode to 28 ; Marksman
    endif

    ; Marksman only normalizes to damage
    if (skillCode == 28 && Recurved.ExpNormalizedBowDamage > 0)
        let damage := GetAttackDamage weapon
        set useIncMod to Recurved.ExpNormalizedBowDamage / damage 
        DebugPrint "recurve: Calculated a bonus experience multiplier of %.2f for equipped bow %n." (1 / useIncMod) weapon
    else
        ; Blade and Blunt normalize exp to speed
        let speed := GetWeaponSpeed weapon
        set expPerSec to Recurved.ExpNormalizedWeaponSpeed / speed
        set useIncMod to 1 / expPerSec
        ; Blade and Blunt normalize exp to dps if set
        if (Recurved.ExpNormalizedWeaponDamage > 0)
            let damage := GetAttackDamage weapon
            set normalizeToDps to Recurved.ExpNormalizedWeaponSpeed * Recurved.ExpNormalizedWeaponDamage
            set dps to speed * damage
            set expDpsMod to dps / normalizeToDps
            set useIncMod to useIncMod / expDpsMod
        endif
        DebugPrint "recurve: Calculated a bonus experience multiplier of %.2f for equipped weapon %n. Weapon's speed of %.2f contributed %.4f experience. Weapon's dps of %.2f multiplied this by an additional %.4f experience." (1 / useIncMod) weapon speed expPerSec dps expDpsMod
    endif
    
	;DebugPrint "recurve: Normalizing weapon type %g. Adding skill use override mod of %.4f for skill %g." weaponType useIncMod skillCode
    ; Remove any existing weapon use overrides
    call rcvDeleteSkillOverrides Recurved.OverrideReasonWeapon
    ; Add the new override
	call rcvAddSkillUseIncOverride skillCode useIncMod useIncMod Recurved.OverrideReasonWeapon
End