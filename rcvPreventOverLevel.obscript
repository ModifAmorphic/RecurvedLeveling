scn rcvPreventOverLevel

int i

; Overlevel variables
int lvlProgress
;int overLvlProgress
int maxLvlProgress
int maxAttrbProgOver
array_var allSkills

; inputs
int addedLevelProgress ; The amount additional progress to add to the existing progress for calculating overlevel protections.
int isScripted         ; Whether or not the increase was caused by a scripted event.

; Outputs
int overLvlProgress

Begin _Function { addedLevelProgress isScripted }

	set overLvlProgress to 0

	set lvlProgress to GetPCMajorSkillUps
	if (isScripted == 0)
		let lvlProgress += addedLevelProgress
	endif
	; Attribute progress needed for the current level
	let maxLvlProgress := (Recurved.LevelCurves[player.GetLevel - 1]["iLevelUpSkillCount"])
	; Attribute progress needed for the level after this, minus 1
	let maxAttrbProgOver := (Recurved.LevelCurves[player.GetLevel]["iLevelUpSkillCount"]) - 1
	DebugPrint "recurve: Starting OverLevel protection processing. Calculated Level Progress: %g/%g." lvlProgress maxLvlProgress
	if (lvlProgress > maxLvlProgress)
		set overLvlProgress to lvlProgress - maxLvlProgress
		DebugPrint "recurve: ExcessAttributeProgress=%g, maxAttrbProgOver=%g." Recurved.ExcessAttributeProgress maxAttrbProgOver
		if (Recurved.ExcessAttributeProgress + overLvlProgress >= maxAttrbProgOver)
			DebugPrint "recurve: Maximum attribute progress over the next level reached. Hardcapping all skill progress until next rest."
			; reset Recurved.SkillUseOverrides since we're about to block every skill from gaining progress
			let Recurved.SkillUseOverrides := ar_Construct Array
			; Hardcap all skills
			let allSkills := ar_Construct Array
			set i to 12
			while (i < 33)
				ar_Append allSkills i
				call rcvAddSkillUseExpOverride i 0 0 Recurved.SkillUseOverrideReasons["Overlevel"]
				let i += 1
			loop
		endif
		; Limit the level progress to the max to prevent adding to the next level
		if (isScripted)
			DebugPrint "recurve: Overlevel Protection - Reseting level progress to %g/%g from %g/%g. Stashed excess progress increases: %g." maxLvlProgress maxLvlProgress lvlProgress maxLvlProgress Recurved.ExcessAttributeProgress
			SetPCMajorSkillUps maxLvlProgress
		else
			DebugPrint "recurve: Overlevel Protection - Requesting delayed reset of level progress to %g/%g from %g/%g. Stashed excess progress increases: %g." maxLvlProgress maxLvlProgress lvlProgress maxLvlProgress Recurved.ExcessAttributeProgress
			set Recurved.DelayedResetLevelProgress to 1
		endif
	endif

	DebugPrint "recurve: PreventOverlevel processed for addedLevelProgress: %g, Progress: %g/%g, maxAttrbProgOver: %g, ExcessAttributeProgress: %g, isScripted: %g, overLvlProgress: %g." addedLevelProgress lvlProgress maxLvlProgress maxAttrbProgOver Recurved.ExcessAttributeProgress isScripted overLvlProgress
	SetFunctionValue overLvlProgress
	
End