scn rcvFindOverridenSkills

array_var useKvp
array_var useOverride
array_var ovrSkill

; inputs
int overrideReason

; ouput
array_var overridenSkills

Begin _Function { overrideReason } ; Gets all skill use overrides for a particular overrideReason.

	let overridenSkills := ar_Construct "Array"
	
	foreach useKvp <- Recurved.SkillUseOverrides
		let useOverride := useKvp["value"]
		if Eval(useOverride["reason"] == overrideReason)
			let ovrSkill := ar_Construct "StringMap"
			let ovrSkill["skillCode"] := useOverride["skillCode"]
			let ovrSkill["overrideReason"] := overrideReason
			let ovrSkill["exp"] := GetPlayerSkillUseC useOverride["skillCode"]
			let ovrSkill["useIncModifier"] := useOverride["useIncModifiers"][0]
			if (overrideReason == Recurved.OverrideReasonAttributeProtection)
				let ovrSkill["penaltyReason"] := Recurved.PenaltyTypeAttributeProtection
			elseif (overrideReason == Recurved.OverrideReasonOverlevel)
				let ovrSkill["penaltyReason"] := Recurved.PenaltyTypeOverleveled
			endif
			ar_Append overridenSkills ovrSkill
		endif
	loop
	SetFunctionValue overridenSkills
End