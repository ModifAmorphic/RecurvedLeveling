scn rcvOnHealthDamageSubscriber


ref target

; inputs
float damage
ref attacker
Begin _Function { damage attacker }
	set target to GetSelf
	if (target != player && attacker != player)
		return
	endif
	if (attacker == 0) ; no extra exp for self harm
		return
	endif

	if (Recurved.ArmorExpFromDamageDisabled == 0)
		if (target == player && attacker != player)
			call rcvApplyBonusArmorExp damage attacker 
			call rcvCaptureDefenseEquipExp
			return
		endif
	endif

	if (attacker != player)
		return
	endif
	
	if (Recurved.AthleticsActionsExpDisabled == 0 && Recurved.AthleticsAttackExpMulti > 0)
		; Only attacks on the ground count towards athletics exp
		if (player.IsInAir == 0)
			if (player.IsPowerAttacking)
				call rcvApplyActionExtraExp Recurved.ActionPowerAttack
			else
				call rcvApplyActionExtraExp Recurved.ActionAttack
			endif
			return
		endif
	endif

	if (Recurved.AcrobaticsActionsExpDisabled == 0 && Recurved.AcrobaticsJumpAttackExpMulti > 0)
		if (player.IsInAir)
			call rcvApplyActionExtraExp Recurved.ActionJumpAttack ; Add jump attack experience
		endif
	endif
	
End