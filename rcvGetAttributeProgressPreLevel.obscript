scn rcvGetAttributeProgressPreLevel

int attributeCode


array_var attributeProgress
int skillProgress
int pointsBalance
; int nextAttributeBonus

; Input Params
array_var currentLevel
array_var levelCurve

; Output array of attributes with progress remaining, if any.
array_var attributePoints
Begin _Function { currentLevel levelCurve } ; Gets any attribute progress remaining prior points being spent (after level up progress has been reached, but before actual level up).
    let attributePoints := ar_Construct "Array"
    set attributeCode to 0
    DebugPrint "recurve: currentLevel attributes: %g" ar_Size currentLevel["attributes"]
    while (attributeCode < ar_Size currentLevel["attributes"])
        if (attributeCode == Recurved.LuckAvc)
			; don't calculate luck progress
			let attributeCode += 1
            continue
        endif

        let skillProgress := currentLevel["attributes"][attributeCode]["skillProgress"]
        let attributeProgress := ar_Construct "StringMap"
        let attributeProgress["attributeCode"] := attributeCode
        set pointsBalance to 0
        ;; only carry over bonus points if configured to do so
        if (Recurved.RemainingAttributeProgressDisabled == 0)
            set pointsBalance to skillProgress
            ; Remove bonus points unless configured to carry forward
            if (Recurved.CarrySecondaryAttributeBonusForward == 0)
                let pointsBalance -= Recurved.SecondaryAttribBonuses[attributeCode]
            endif
            if (Recurved.CarryMajorAttributeBonusForward == 0)
                let pointsBalance -= Recurved.PrimaryAttribBonuses[attributeCode]
            endif		
            if (pointsBalance < 0)
                set pointsBalance to 0
            endif
        endif
        let attributeProgress["progressPoints"] := pointsBalance
        DebugPrint "recurve: Calculated %g points remaining for attribute %g adjusted from raw progress %g.  Primary Bonus points: %g, Secondary Bonus Points: %g." attributeProgress["progressPoints"] attributeCode skillProgress Recurved.PrimaryAttribBonuses[attributeCode] Recurved.SecondaryAttribBonuses[attributeCode]
        ar_Append attributePoints attributeProgress

        let attributeCode += 1
    loop
    SetFunctionValue attributePoints
End