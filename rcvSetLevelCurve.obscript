scn rcvSetLevelCurve

int i
array_var skill
array_var lvlUpMult
string_var levelUpMultSetting
;inputs
array_var levelCurve
array_var useOverrides
array_var useOverride
Begin _Function { levelCurve useOverrides }
	set i to 0
	while (i < ar_Size levelCurve["iLevelUpMults"])
		let lvlUpMult := levelCurve["iLevelUpMults"][i]
		let levelUpMultSetting := lvlUpMult["setting"]
		SetNumericGameSetting $levelUpMultSetting lvlUpMult["value"]
		;DebugPrint "recurve: Set %z to %g." levelUpMultSetting lvlUpMult["value"]
		let i += 1
	loop

	set i to 0
	while (i < ar_Size levelCurve["skills"])
		let skill := levelCurve["skills"][i]
		if Eval (skill["softCapEnabled"] == 1)
			SetSkillUseIncrementC skill["softCapUseExp"][0] skill["skillCode"] 0
			SetSkillUseIncrementC skill["softCapUseExp"][1] skill["skillCode"] 1
			DebugPrint "recurve: Applied softcap to skill %g. Set UseExp0=%.4f, UseExp1=%.4f." skill["skillCode"] skill["softCapUseExp"][0] skill["softCapUseExp"][1]
		endif
		let i += 1
	loop
	DebugPrint "recurve: Set iLevelUpSkillCount to %g." levelCurve["iLevelUpSkillCount"]
	SetNumericGameSetting "iLevelUpSkillCount" levelCurve["iLevelUpSkillCount"]
	
	set i to 0
	while (i < ar_Size useOverrides)
		let useOverride := useOverrides[i]
		SetSkillUseIncrementC useOverride["useExp"][0] useOverride["skillCode"] 0
		SetSkillUseIncrementC useOverride["useExp"][1] useOverride["skillCode"] 1
		DebugPrint "recurve: Overrode skill %g use increments for reason %g. Set UseExp0=%.4f, UseExp1=%.4f." (useOverrides[i]["skillCode"]) (useOverrides[i]["reason"]) (useOverride["useExp"][0]) (useOverride["useExp"][1])
		let i += 1
	loop
	set i to 0
End