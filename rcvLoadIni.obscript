scn rcvLoadIni

; params
Begin _Function { } ; Loads ini file, setting default values first.
	; ### Load Default Values ###
	call rcvLoadIniDefaults
	
	;run batch ini settings script
	if FileExists "Data\Recurved.ini"
		RunBatchScript "Data\Recurved.ini"
		if Recurved.DebugEnabled
			SetDebugMode 1
		endif
		DebugPrint "recurve: Ini file Data\Recurved.ini loaded."
	else
		PrintToConsole "recurve: Data\Recurved.ini not found."
	endif
	
	; Set expiration for event pruning
	let Recurved.EventExpirationTicks := call rcvConvertSecondsToTicks Recurved.EventExpirationSeconds
	DebugPrint "recurve: Set EventExpirationTicks to %g from EventExpirationSeconds value of %g." Recurved.EventExpirationTicks Recurved.EventExpirationSeconds
	
	; Read secondaries into array for easier access access
	let Recurved.AttributeSecondaries := ar_Construct "Array"
	let Recurved.AttributeSecondaries[0] := Recurved.StrengthSecondary
	let Recurved.AttributeSecondaries[1] := Recurved.IntelligenceSecondary
	let Recurved.AttributeSecondaries[2] := Recurved.WillpowerSecondary
	let Recurved.AttributeSecondaries[3] := Recurved.AgilitySecondary
	let Recurved.AttributeSecondaries[4] := Recurved.SpeedSecondary
	let Recurved.AttributeSecondaries[5] := Recurved.EnduranceSecondary
	let Recurved.AttributeSecondaries[6] := Recurved.PersonalitySecondary
End